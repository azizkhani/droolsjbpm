package org.drools.solver.examples.itc2007.examination.solver;
    dialect "java"

import org.drools.solver.core.score.calculator.HardAndSoftConstraintScoreCalculator;
import org.drools.solver.core.score.constraint.IntConstraintOccurrence;
import org.drools.solver.core.score.constraint.ConstraintType;

import org.drools.solver.examples.patientadmissionschedule.domain.AdmissionPart;
import org.drools.solver.examples.patientadmissionschedule.domain.Bed;
import org.drools.solver.examples.patientadmissionschedule.domain.BedDesignation;
import org.drools.solver.examples.patientadmissionschedule.domain.Department;
import org.drools.solver.examples.patientadmissionschedule.domain.DepartmentSpecialism;
import org.drools.solver.examples.patientadmissionschedule.domain.Equipment;
import org.drools.solver.examples.patientadmissionschedule.domain.Gender;
import org.drools.solver.examples.patientadmissionschedule.domain.GenderLimitation;
import org.drools.solver.examples.patientadmissionschedule.domain.Night;
import org.drools.solver.examples.patientadmissionschedule.domain.Patient;
import org.drools.solver.examples.patientadmissionschedule.domain.PatientAdmissionSchedule;
import org.drools.solver.examples.patientadmissionschedule.domain.PreferredPatientEquipment;
import org.drools.solver.examples.patientadmissionschedule.domain.RequiredPatientEquipment;
import org.drools.solver.examples.patientadmissionschedule.domain.Room;
import org.drools.solver.examples.patientadmissionschedule.domain.RoomEquipment;
import org.drools.solver.examples.patientadmissionschedule.domain.RoomSpecialism;
import org.drools.solver.examples.patientadmissionschedule.domain.Specialism;
import org.drools.solver.examples.patientadmissionschedule.domain.solver.AdmissionPartConflict;

// @author Geoffrey De Smet

global HardAndSoftConstraintScoreCalculator scoreCalculator;

// ############################################################################
// Hard constraints
// ############################################################################

// RoomMaintenance constraint is not yet applicable

// Two patients in the same bed for a number of nights.
rule "sameBedInSameNight"
    when
        $admissionPartConflict : AdmissionPartConflict($leftAdmissionPart : leftAdmissionPart,
                $rightAdmissionPart : rightAdmissionPart);
        $leftBedDesignation : BedDesignation(admissionPart == $leftAdmissionPart, $bed : bed);
        $rightBedDesignation : BedDesignation(admissionPart == $rightAdmissionPart, bed == $bed);
    then
        insertLogical(new IntConstraintOccurrence("sameBedInSameNight",
                ConstraintType.NEGATIVE_HARD, $admissionPartConflict.getNightSize(),
                $leftBedDesignation, $rightBedDesignation));
end

// Different genders in the same room when the room doesn't allow it.
rule "differentGenderInSameGenderRoomInSameNight"
    when
        $admissionPartConflict : AdmissionPartConflict($leftAdmissionPart : leftAdmissionPart,
                $rightAdmissionPart : rightAdmissionPart,
                differentGender == true);
        $leftBedDesignation : BedDesignation(roomGenderLimitation == GenderLimitation.SAME_GENDER,
                admissionPart == $leftAdmissionPart, $room : room);
        $rightBedDesignation : BedDesignation(room == $room, admissionPart == $rightAdmissionPart);
    then
        insertLogical(new IntConstraintOccurrence("differentGenderInSameGenderRoomInSameNight",
                ConstraintType.NEGATIVE_HARD, $admissionPartConflict.getNightSize(),
                $leftBedDesignation, $rightBedDesignation));
end

// Room's GenderLimitation male only and female only constraint is build-in

// Departement's mininumAge and maximumAge constraint is build-in

// RequiredPatientEquipment constraint is build-in

// Patient's requiredRoomCapacity constraint is not yet applicable

// Accumulate hard constraints
rule "hardConstraintsBroken"
        salience -1 // Finish the other rules first (optional, for performance)
    when
        $hardTotal : Number() from accumulate(
            IntConstraintOccurrence(constraintType == ConstraintType.NEGATIVE_HARD, $weight : weight),
            sum($weight) // Vote for http://jira.jboss.com/jira/browse/JBRULES-1075
        );
    then
        scoreCalculator.setHardConstraintsBroken($hardTotal.intValue());
end

// ############################################################################
// Soft constraints
// ############################################################################


// DepartmentSpecialism constraint
rule "departmentSpecialism"
    when
        $bedDesignation : BedDesignation($specialism : admissionPartSpecialism, $department : department);
        not DepartmentSpecialism(department == $department, specialism == $specialism);
    then
        insertLogical(new IntConstraintOccurrence("departmentSpecialism",
                ConstraintType.NEGATIVE_SOFT, 10,
                $bedDesignation));
end

// RoomSpecialism constraint
rule "roomSpecialism"
    when
        $bedDesignation : BedDesignation($specialism : admissionPartSpecialism, $room : room);
        not RoomSpecialism(room == $room, specialism == $specialism);
    then
        insertLogical(new IntConstraintOccurrence("roomSpecialism",
                ConstraintType.NEGATIVE_SOFT, 10,
                $bedDesignation));
end

// PreferredPatientEquipment constraint
rule "preferredPatientEquipment"
    when
        $preferredPatientEquipment : PreferredPatientEquipment($patient : patient, $equipment : equipment);
        $bedDesignation : BedDesignation(patient == $patient, $room : room);
        not RoomEquipment(room == $room, equipment == $equipment);
    then
        insertLogical(new IntConstraintOccurrence("preferredPatientEquipment",
                ConstraintType.NEGATIVE_SOFT, 20,
                $bedDesignation));
end

// Do not change bed in an AdmissionPart on different Nights constraint is build-in

// Accumulate soft constraints
rule "softConstraintsBroken"
        salience -1 // Finish the other rules first (optional, for performance)
    when
        $softTotal : Number() from accumulate(
            IntConstraintOccurrence(constraintType == ConstraintType.NEGATIVE_SOFT, $weight : weight),
            sum($weight) // Vote for http://jira.jboss.com/jira/browse/JBRULES-1075
        );
    then
        scoreCalculator.setSoftConstraintsBroken($softTotal.intValue());
end
